apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.jakewharton.butterknife'
apply plugin: 'com.squareup.sqldelight'

android {
    compileSdkVersion rootProject.ext.androidCompileSdkVersion
    buildToolsVersion rootProject.ext.androidBuildToolsVersion

    defaultConfig {
        applicationId "com.github.okbuilds.okbuck.example"
        minSdkVersion rootProject.ext.androidMinSdkVersion
        targetSdkVersion rootProject.ext.androidTargetSdkVersion
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        versionCode 1
        versionName "1.0"
        buildConfigField "boolean", "CAN_JUMP", "true"
        buildConfigField "boolean", "XLOG_ENABLED", "true"
        buildConfigField "int", "EXOPACKAGE_FLAGS", "0"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {
            storeFile file('config/keystore/debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }

    productFlavors {
        dev {
            buildConfigField "boolean", "CAN_JUMP", "true"
            applicationIdSuffix = ".dev"
            buildConfigField "int", "EXOPACKAGE_FLAGS", "1"
        }
        prod {
            buildConfigField "boolean", "CAN_JUMP", "false"
            applicationIdSuffix = ".prod"
        }
        demo {
            buildConfigField "boolean", "CAN_JUMP", "false"
            applicationIdSuffix = ".demo"
            buildConfigField "int", "EXOPACKAGE_FLAGS", "1"
        }
    }

    buildTypes {
        debug {
            buildConfigField "boolean", "DEBUG_CONFIG", "true"
            signingConfig signingConfigs.release
            applicationIdSuffix = ".debug"
        }
        release {
            buildConfigField "boolean", "CAN_JUMP", "true"
            buildConfigField "boolean", "DEBUG_CONFIG", "false"
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt')
        }
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    lintOptions {
        abortOnError false
    }
}

repositories {
    flatDir {
        dirs 'aars'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile(name:'rxscreenshotdetector-release', ext:'aar')
    compile 'io.reactivex:rxjava:1.1.0'
    compile ('io.reactivex:rxandroid:1.1.0') {
        exclude module: 'rxjava'
    }
    compile 'com.tbruyelle.rxpermissions:rxpermissions:0.5.2@aar'

    compile 'com.android.support:multidex:1.0.1'
    compile 'com.jakewharton:butterknife:8.4.0'
    apt 'com.jakewharton:butterknife-compiler:8.4.0'

    compile("com.android.support:design:${rootProject.ext.androidSupportSdkVersion}") {
        exclude module: 'appcompat-v7'
    }
    compile "com.android.support:recyclerview-v7:${rootProject.ext.androidSupportSdkVersion}"
    compile "com.squareup.leakcanary:leakcanary-android:1.3.1"

    apt 'com.google.dagger:dagger-compiler:2.5'
    apt "com.ryanharter.auto.value:auto-value-gson:0.3.1"
    apt "com.google.auto.value:auto-value:1.2"
    // used for AndroidStudio sync
    provided "com.google.auto.value:auto-value:1.2"

    devCompile project(path: ':dummylibrary', configuration: 'freeRelease')
    prodCompile project(path: ':dummylibrary', configuration: 'paidRelease')
    demoCompile project(path: ':dummylibrary', configuration: 'paidRelease')
    compile project(':libraries:emptylibrary')

    compile 'com.github.promeg:xlog-android-idle:2.1.1'

    testCompile project(path: ':dummylibrary', configuration: 'freeDebug')
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.9.5'
    testCompile 'com.android.support:support-annotations:24.0.0'
}

afterEvaluate {
    tasks.withType(JavaCompile) { JavaCompile javaCompile ->
        javaCompile.options.compilerArgs << "-Xlint:deprecation"
    }
}
