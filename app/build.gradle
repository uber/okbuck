apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.jakewharton.butterknife'
apply plugin: 'com.squareup.sqldelight'

android {
    defaultConfig {
        applicationId "com.uber.okbuck.example"
        multiDexEnabled true
        buildConfigField "boolean", "CAN_JUMP", "true"
        buildConfigField "boolean", "XLOG_ENABLED", "true"
        buildConfigField "int", "EXOPACKAGE_FLAGS", "0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    productFlavors {
        dev {
            buildConfigField "boolean", "CAN_JUMP", "true"
            applicationIdSuffix = ".dev"
            buildConfigField "int", "EXOPACKAGE_FLAGS", "1"
        }
        prod {
            buildConfigField "boolean", "CAN_JUMP", "false"
            applicationIdSuffix = ".prod"
        }
        demo {
            buildConfigField "boolean", "CAN_JUMP", "false"
            applicationIdSuffix = ".demo"
            buildConfigField "int", "EXOPACKAGE_FLAGS", "1"
        }
    }

    buildTypes {
        debug {
            buildConfigField "boolean", "DEBUG_CONFIG", "true"
            applicationIdSuffix = ".debug"
        }
        release {
            buildConfigField "boolean", "CAN_JUMP", "true"
            buildConfigField "boolean", "DEBUG_CONFIG", "false"
        }
    }
}

repositories {
    flatDir {
        dirs 'aars'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile(name: 'rxscreenshotdetector-release', ext: 'aar')
    compile deps.external.rxjava
    compile deps.external.rxandroid
    compile deps.external.rxPermissions
    compile deps.support.multidex
    compile deps.external.butterKnife
    compile deps.support.design
    compile deps.support.recyclerView
    compile deps.external.leakCanary
    compile deps.external.xlogAndroidIdle

    compile project(':libraries:emptylibrary')
    devCompile project(path: ':dummylibrary', configuration: 'freeRelease')
    prodCompile project(path: ':dummylibrary', configuration: 'paidRelease')
    demoCompile project(path: ':dummylibrary', configuration: 'paidRelease')

    apt deps.apt.autoValue
    apt deps.apt.butterKnifeCompiler
    annotationProcessor deps.apt.daggerCompiler
    annotationProcessor deps.apt.autoValueGson

    testCompile project(path: ':dummylibrary', configuration: 'freeDebug')
    testCompile deps.test.junit
    testCompile deps.test.mockito
    testCompile deps.support.annotations

    androidTestCompile(deps.test.espressoCore) {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile(deps.test.espressoRunner) {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
}

afterEvaluate {
    tasks.withType(JavaCompile) { JavaCompile javaCompile ->
        javaCompile.options.compilerArgs << "-Xlint:deprecation"
    }
}
