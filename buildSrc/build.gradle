plugins {
    id "groovy"
    id "maven-publish"
    id "com.jfrog.bintray" version "1.7"
    id "com.fizzed.rocker" version "0.22.0"
}

apply from: "../dependencies.gradle"

repositories {
    jcenter()
    google()
}

jar {
    exclude('**/*.rocker.raw')
}

tasks.withType(GroovyCompile) {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

tasks.withType(JavaCompile) {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

sourceSets.main.java.srcDirs = ["src/main/rocker"]
sourceSets.main.groovy.srcDirs = ["src/main/java", "src/main/groovy"]
sourceSets.main.rocker.srcDirs = ["src/main/rocker", "src/main/java"]

dependencies {
    compile gradleApi()
    compile localGroovy()

    compile deps.build.androidPlugin
    compile deps.build.commonsIo
    compile deps.build.commonsLang
    compile deps.build.kotlinPlugin
    compile deps.build.mavenArtifact
    compile deps.build.okio
    compile deps.build.rockerRuntime

    testCompile deps.test.junit
}

rocker {
    discardLogicWhitespace true
    optimize true
    extendsClass "com.uber.okbuck.template.core.RuleTemplate"
    extendsModelClass "com.uber.okbuck.template.core.Rule"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

def publishVersion = '0.31.2'
group = 'com.uber'
version = publishVersion
def siteUrl = 'https://github.com/uber/okbuck'
def gitUrl = 'https://github.com/uber/okbuck.git'

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar
            artifact javadocJar

            artifactId 'okbuck'
        }
    }
}

bintray {
    user = project.properties.get("bintray.user")
    key = project.properties.get("bintray.apikey")
    publications = ['mavenJava']
    dryRun = false
    publish = true
    pkg {
        repo = 'gradle-plugins'
        name = 'okbuck'
        userOrg = 'uber'
        desc = 'Gradle plugin that lets developers utilize the Buck build system on a Gradle project'
        websiteUrl = siteUrl
        issueTrackerUrl = siteUrl + '/issues'
        vcsUrl = gitUrl
        licenses = ['MIT']
        labels = ['buck', 'okbuck', 'uber']
        publicDownloadNumbers = true
        version {
            name = publishVersion
            released = new java.text.SimpleDateFormat('yyyy-MM-dd\'T\'HH:mm:ss.SSSZZ').format(new Date())
            vcsTag = publishVersion
        }
    }
}
