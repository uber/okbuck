import java.text.SimpleDateFormat

apply plugin: 'maven-publish'
apply plugin: "com.jfrog.bintray"
apply plugin: 'maven'
apply plugin: 'groovy'

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.3.1"
        classpath 'com.github.dcendents:android-maven-plugin:1.2'
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

tasks.withType(JavaCompile) {
    sourceCompatibility = '1.7'
    targetCompatibility = '1.7'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    compile 'commons-io:commons-io:2.4'
    compile 'commons-lang:commons-lang:2.6'
    compile 'org.apache.maven:maven-artifact:3.3.9'
    compile 'com.android.tools.build:gradle:2.2.0-rc2'

    testCompile 'junit:junit:4.12'
}

def siteUrl = 'https://github.com/OkBuilds/OkBuck'
def gitUrl = 'https://github.com/OkBuilds/OkBuck.git'
def publishGroup = 'com.github.okbuilds'
def publishVersion = '0.5.0'

group = publishGroup
version = publishVersion

Properties properties = new Properties()
// for wrapper project structure
File propertyFile = project.rootProject.file('buildSrc/bintray.properties')
if (propertyFile.exists()) {
    properties.load(propertyFile.newDataInputStream())
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar
            artifact javadocJar

            artifactId 'okbuild-gradle-plugin'
        }
    }
}

bintray {
    user = project.properties.get("bintray.user", properties.getProperty("bintray.user"))
    key = project.properties.get("bintray.apikey", properties.getProperty("bintray.apikey"))
    publications = ['mavenJava']
    dryRun = false
    publish = true
    pkg {
        repo = 'maven'
        name = 'OkBuild'
        userOrg = 'okbuild'
        desc = 'Gradle plugin to convert build to buck'
        websiteUrl = siteUrl
        issueTrackerUrl = siteUrl + '/issues'
        vcsUrl = gitUrl
        licenses = ['MIT']
        labels = ['buck', 'okbuck', 'okbuild']
        publicDownloadNumbers = true
        version {
            name = publishVersion
            released = new SimpleDateFormat('yyyy-MM-dd\'T\'HH:mm:ss.SSSZZ').format(new Date())
            vcsTag = publishVersion
        }
    }
}
